ptc_twiss_beta_macro(location): macro={
  ptc_create_universe;
  ptc_create_layout,time=false,model=2,method=6,nst=3,exact;
  select,flag=ptc_twiss, clear;
  select,flag=ptc_twiss, column=name, s,x,px,beta11,beta22,alfa11,alfa22,mu1,mu2;
  ptc_twiss,closed_orbit;
  betax_ref=table(ptc_twiss,location,beta11);
  betay_ref=table(ptc_twiss,location,beta22);
  ptc_end;
};

ptc_twiss_tune_macro: macro={
  ptc_create_universe;
  ptc_create_layout, time=false, model=2, exact=true, method=6, nst=5;
  ptc_twiss, closed_orbit, table=ptc_twiss,icase=56,no=2,summary_table=ptc_twiss_summary;
  qx0=table(ptc_twiss_summary,Q1);
  qy0=table(ptc_twiss_summary,Q2);
  value, qx0, qy0;
  ptc_end;
};

ptc_twiss_chroma_macro: macro={
  ptc_create_universe;
  ptc_create_layout, time=false, model=2, exact=true, method=6, nst=5;
  ptc_twiss, closed_orbit, table=ptc_twiss,icase=56,no=2,summary_table=ptc_twiss_summary;
  qpx0=table(ptc_twiss_summary,DQ1);
  qpy0=table(ptc_twiss_summary,DQ2);
  value, qpx0, qpy0;
  ptc_end;
};

ptc_twiss_Q2P_macro: macro={
  ptc_create_universe;
  ptc_create_layout, time=false, model=2, exact=true, method=6, nst=5;
  ptc_twiss, closed_orbit,icase=56,no=4,normal;
  qpx20 = table(nonlin,value,7);
  qpy20 = table(nonlin,value,15);
  value, qpx20, qpy20;
  ptc_end;
};

match_LEQ: macro={
use, sequence=PS;
match, use_macro;
  vary,   name=kf, step=1.0E-6 ;
  vary,   name=kd, step=1.0E-6 ;  
  USE_MACRO, name=ptc_twiss_tune_macro;
  CONSTRAINT, expr=  table(ptc_twiss_summary,Q1)= Qx;
  CONSTRAINT, expr=  table(ptc_twiss_summary,Q2)= Qy;
  JACOBIAN,calls=10000,bisec=3,TOLERANCE=1.0E-21;
ENDMATCH;
};

match_XNO: macro={
use, sequence=PS;
match, use_macro;
  vary,   name=sd, step=1.0E-6 ; 
  vary,   name=sf, step=1.0E-6 ;   
  USE_MACRO, name=ptc_twiss_chroma_macro;
  CONSTRAINT, expr=  table(ptc_twiss_summary,DQ1)= Qpx;  
  CONSTRAINT, expr=  table(ptc_twiss_summary,DQ2)= Qpy;
  JACOBIAN,calls=10000,bisec=3,TOLERANCE=1.0E-21;
ENDMATCH;
};

ptc_chrom_macro: macro={
  ptc_create_universe;
  ptc_create_layout,time=false,model=2,method=6,nst=3,exact=true;
  select_ptc_normal, q1=0, q2=0;
  select_ptc_normal,dq1=1,dq2=1;
  select_ptc_normal,dq1=2,dq2=2;
  select_ptc_normal,dq1=3,dq2=3;
  ptc_normal,closed_orbit,normal,icase=5,no=4;
  ptc_end;
  qx0 =table(normal_results,value,1);
  qx1 =table(normal_results,value,3);
  qx2 =table(normal_results,value,5);
  qx3 =table(normal_results,value,7);
  qy0 =table(normal_results,value,2);
  qy1 =table(normal_results,value,4);
  qy2 =table(normal_results,value,6);
  qy3 =table(normal_results,value,8);
  value, qx0, qx1, qx2, qx3, qy0, qy1, qy2, qy3;
};

ptc_chrom_macro2: macro={
  ptc_create_universe;
  ptc_create_layout, time=false, model=2, method=6, nst=5, exact=true,;
  ptc_twiss,closed_orbit,icase=56,normal,no=4;
  ptc_end;
  qx0 = table(nonlin,value,1);
  qx1 = table(nonlin,value,4)*beam->beta;
  qx2 = table(nonlin,value,7)*beam->beta*beam->beta;
  qx3 = table(nonlin,value,8)*beam->beta*beam->beta*beam->beta;

  qy0 = table(nonlin,value,9);
  qy1 = table(nonlin,value,12)*beam->beta;
  qy2 = table(nonlin,value,15)*beam->beta*beam->beta;
  qy3 = table(nonlin,value,16)*beam->beta*beam->beta*beam->beta;
  value, qx0, qx1, qx2, qx3, qy0, qy1, qy2, qy3;
};